//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

EventPointerTable(0x2D,ThisChapter)

NPCEscapePointPointerTable(0xA, Roy1EscapePoint)
Roy1EscapePoint:
EscapePoint(0, 0, MoveUp)
EscapePointEnd

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN Units Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0,CordeliaSamsonArrive,4)
TurnEventPlayer(0, GazelleEnters,6)
TurnEventPlayer(0, BinksAggroes, 8)
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
CharacterEvent(0x7,RoyRecruitsMaria,Roy,Maria)
CharacterEvent(0x8, MariaRecruitsSamson, Maria, Samson)
CharacterEvent(0x9, MariaRecruitsCordelia, Maria, Cordelia)
CharacterEvent(0x9, SamsonRecruitsCordelia, Samson, Cordelia)
CharacterEvent(0x8, CordeliaRecruitsSamson, Cordelia, Samson)
CharacterEvent(0xA, GazelleJoins, Roy, Gazelle)
//CharacterEvent(0x0, Skippo, Roy, Alan)
END_MAIN

LocationBasedEvents:
Village(0xB, R1LeftHouse, 1,9)
Village(0xC, R1RightHouse, 2,9)
Armory(ArmoryR1,13,2)
Vendor(ShopR1,12,3)
END_MAIN

ArmoryR1:
SHLI IronSword SteelSword IronBlade Armourslayer IronLance SteelLance Javelin IronAxe SteelAxe HandAxe Hammer IronBow SteelBow Longbow

ShopR1:
SHLI Fire Thunder Elfire Lightning Shine Flux Luna Nosferatu Heal Mend Unlock Barrier Vulnerary

ALIGN 4

MiscBasedEvents:
//AREA 0x6 EndingScene [4,3] [5,3]
//DefeatBoss(EndingScene)
DefeatAll(EndingScene)
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

Units:
UNIT Roy RoyLord 0x0 Level(1, Ally, 0) [3,3] 0x00 0x00 0x0 0x00 [Rapier, Vulnerary] NoAI
UNIT Lukas Cavalier 0x0 Level(4,Ally,0) [2,4] 0x00 0x00 0x0 0x00 [Longsword,Javelin] NoAI
UNIT Alan Cavalier 0x0 Level(4, Ally, 0) [4,4] 0x00 0x00 0x0 0x00 [SteelSword, IronLance, Vulnerary] NoAI
UNIT Dorcas Fighter 0x0 Level(4, Ally, 0) [4,2] 0x00 0x00 0x0 0x00 [SteelAxe,HandAxe,Vulnerary] NoAI
UNIT Anna PegasusKnight 0x0 Level(3, Ally, 0) [2,2] 0x00 0x00 0x0 0x00 [IronLance, GoddessIcon] NoAI
UNIT Salem Monk 0x0 Level(2, Ally, 0) [3,1] 0x00 0x00 0x0 0x00 [Lightning, Shine] NoAI
UNIT Priscilla Troubadour 0x0 Level(2, Ally, 0) [1,3] 0x00 0x00 0x0 0x00 [Heal, Vulnerary] NoAI
UNIT Leila Thief 0x0 Level(2,Ally,0) [1,1] 0x00 0x00 0x0 0x00 [IronSword,Lockpick] NoAI
UNIT Leila Thief 0x0 Level(2,Ally,0) [2,1] 0x00 0x00 0x0 0x00 [IronSword,Lockpick] NoAI
UNIT Leila Thief 0x0 Level(2,Ally,0) [1,2] 0x00 0x00 0x0 0x00 [IronSword,Lockpick] NoAI
UNIT

//0x92 is standard, 0x93 is lunge, 0x94 is Pass, 0x95 is for rush on Gazelle arrival, 0x96 and 0x97 is Gazelles

Enemies:
UNIT Binks WyvernLord 0x0 Level(1,Enemy,0) [15,16] 0 0 0 0 [SteelAxe, Javelin] GuardTileAI
UNIT 0x93 WyvernRider 0x0 Level(10,Enemy,1) [11,2] 0 0 0 0 [SteelLance] DefaultAI	
UNIT 0x94 PegasusKnight 0x0 Level(10,Enemy,1) [6,16] 0 0 0 0 [FlyerLance] DefaultAI
UNIT 0x92 Mercenary 0x0 Level(8,Enemy,1) [12,13] 0 0 0 0 [SteelSword] DefaultAI
UNIT 0x92 Cavalier 0x0 Level(8, Enemy,1) [0,14] 0 0 0 0 [SteelSword] DefaultAI
UNIT 0x92 Cavalier 0x0 Level(8,Enemy,1) [0,15] 0 0 0 0 [SteelSword] DefaultAI
UNIT 0x92 Archer 0x0 Level(8,Enemy,1) [5,9] 0 0 0 0 [SteelBow] DefaultAI
UNIT 0x95 Fighter 0x0 Level(8,Enemy,1) [2,10] 0 0 0 0 [SteelAxe] DefaultAI
UNIT 0x92 Mercenary 0x0 Level(8,Enemy,1) [5,12] 0 0 0 0 [SteelSword] DefaultAI
UNIT 0x94 PegasusKnight 0x0 Level(8,Enemy,1) [14,4] 0 0 0 0 [IronLance] DefaultAI
UNIT 0x95 Mercenary 0x0 Level(8,Enemy,1) [14,2] 0 0 0 0 [SteelSword] AttackInRangeAI
//UNIT 0x94 PegasusKnight 0x0 Level(8,Enemy,1) [15,5] 0 0 0 0 [IronLance] DefaultAI
UNIT 

MoreEnemies:
UNIT 0x92 AxeCavalier 0x0 Level(8, Enemy, 1) [13, 16] 0 0 0 0 [SteelAxe] DefaultAI
UNIT 0x92 LanceCavalier 0x0 Level(8,Enemy,1) [14,15] 0 0 0 0 [SteelLance] DefaultAI
UNIT

Runaway:
UNIT Maria Cleric 0x0 Level(4,NPC,0) [9,9] 0 0 0 0 [Mend, Barrier, Rescue] [0x6,0xC,0x0,0x0]
UNIT

Bodyguards:
UNIT Cordelia PegasusKnight 0x0 Level(7,NPC, 0) [0,16] 0 0 0 0 [FlyerLance, Axereaver, Vulnerary] DefaultAI
UNIT Samson Fighter 0x0 Level(8,NPC, 0) [1,17] 0 0 0 0 [SteelAxe, DragonAxe] DefaultAI
UNIT 0x92 Fighter 0x0 Level(8,Enemy,1) [3,10] 0 0 0 0 [IronAxe] DefaultAI
UNIT 0x93 WyvernRider 0x0 Level(8,Enemy,1) [11,2] 0 0 0 0 [SteelLance] DefaultAI
UNIT 0x92 Cavalier 0x0 Level(8,Enemy,1) [15,17] 0 0 0 0 [SteelSword] DefaultAI
UNIT 0x92 AxeCavalier 0x0 Level(8, Enemy,1) [16,16] 0 0 0 0 [SteelAxe] DefaultAI
UNIT

JustSamson:
UNIT Samson Fighter 0x0 Level(8,Ally, 0) [1,17] 0 0 0 0 [SteelAxe, DragonAxe] DefaultAI
UNIT

JustCordelia:
UNIT Cordelia PegasusKnight 0x0 Level(7,Ally, 0) [0,16] 0 0 0 0 [FlyerLance, Axereaver, Vulnerary] DefaultAI
UNIT

LateBodyguards:
UNIT Cordelia PegasusKnight 0x0 Level(7,Ally, 0) [0,16] 0 0 0 0 [FlyerLance, Axereaver, Vulnerary] DefaultAI
UNIT Samson Fighter 0x0 Level(8,Ally, 0) [1,17] 0 0 0 0 [SteelAxe, DragonAxe] DefaultAI
UNIT

SoloGazelle:
UNIT Gazelle Ranger 0x0 Level(1,Ally,0) [16,0] 0 0 0 0 [SteelBow, SteelSword] DefaultAI
UNIT

Mercenaries:
UNIT Gazelle Ranger 0x0 Level(1,Enemy,0) [16,0] 0 0 0 0 [SteelBow, SteelSword] DefaultAI
UNIT 0x96 Cavalier 0x0 Level(10, Enemy,1) [15,0] 0 0 0 0 [SteelSword, Lancereaver] DefaultAI
UNIT 0x97 AxeCavalier 0x0 Level(10,Enemy,1) [16,1] 0 0 0 0 [KillerAxe, HandAxe] DefaultAI
UNIT 

T8Reinforcements:
UNIT 0x93 WyvernRider 0x0 Level(8,Enemy,1) [15,17] 0 0 0 0 [Javelin] DefaultAI
UNIT 0x93 WyvernRider 0x0 Level(8,Enemy,1) [16,16] 0 0 0 0 [Javelin] DefaultAI
UNIT 0x94 PegasusKnight 0x0 Level(8,Enemy,1) [6,16] 0 0 0 0 [FlyerLance] DefaultAI
UNIT 0x92 Myrmidon 0x0 Level(12, Enemy,1) [0,17] 0 0 0 0 [SilverSword] DefaultAI
UNIT

RoyRecruitsMaria:
Text(RoyRecruitsMariaText)
TurnAlly(Maria)
ENDA

CordeliaSamsonArrive:
CAM1 [4,13]
LOAD1 0x1 Bodyguards
Text(CordeliaSamsonEnterTheMap)
ENDA

MariaRecruitsSamson:
Text(MariaRecruitsSamsonText)
TurnAlly(Samson)
ENDA

MariaRecruitsCordelia:
Text(MariaRecruitsCordeliaText)
TurnAlly(Cordelia)
ENDA

SamsonRecruitsCordelia:
TurnAlly(Cordelia)
ENDA

CordeliaRecruitsSamson:
TurnAlly(Samson)
ENDA

BinksAggroes:
Text(BinksIsNowAngryText)
SVAL 0x1 0x00000000
CHAI Binks
ReinforcementEvent(T8Reinforcements)
ENDA

GazelleEnters:

SETVAL 0x1 0x00000000
CHAI(0x95)
ReinforcementEvent(Mercenaries)
ENDA

R1LeftHouse:
VillageEventMoney(R1LeftHouseText,HouseBG,5000)
ENDA

R1RightHouse:
VillageEventItem(R1RightHouseText,HouseBG,KillerBow)
ENDA

GazelleJoins:
TEXTSTART
TEXTSHOW RoyRecruitsGazelle
TEXTEND
SETVAL 0x4 4000
CHECK_MONEY
BGE 0x0 0xC 0x4
	//if you don't have money
	TEXTSHOW2 RoyGazelleBrokeBoi
	TEXTEND
	GOTO 0x1
LABEL 0x0
	//if you got cash
	EVBIT_F 0x3
	TEXTSHOW2 RoyGazelleGotMoney
	TEXTEND
	SETVAL 0x7 0x1
	BEQ 0x2 0xC 0x7 //if yes, jumps to label 2
		TEXTSHOW2 RoyGazelleBrokeBoi
		TEXTEND		
	GOTO 0x1
	LABEL 0x2
		SADD 0x32
		SADD 0x43
		GIVETOSLOT3
		TEXTSHOW2 RoyGazelleNowBrokeBoi
		TEXTEND
		TurnAlly(Gazelle)
		CHECK_ALIVE 0x96
		SVAL 0x3 0x1
		BNE 0x4 0x3 0xC
			TurnNPC(0x96)
		LABEL 0x4
		CHECK_ALIVE 0x97
		SVAL 0x3 0x1
		BNE 0x5 0x3 0xC
			TurnNPC(0x97)
		LABEL 0x5
	LABEL 0x1
REMA
ENDA

GazelleIsLate:
Text(Plain_2, EarlyFinishGazelle)
LOAD1 0x1 SoloGazelle
ENDA

BeginningScene:
LOAD2 0x1 Units
ENUN
LOAD1 0x1 Enemies
ENUN
LOAD1 0x1 MoreEnemies
ENUN
LOAD1 0x1 Runaway
ENUN
GotoPrepScreen
NoFade
ENDA

Skippo:
CALL EndingScene
ENDA

EndingScene:
MUSC Victory
//If finish before Cordelia and Samson spawn
/*SVAL 0x3 0x3
CHECK_TURNS
BLE 0xA 0x3 0xC
	LOAD1 0x1 LateBodyguards
	GOTO 0x8
LABEL 0xA*/

SVAL s3 4
CHECK_TURNS
BGE 0xA sC s3
	LOAD1 0x1 LateBodyguards
	GOTO 0x8
LABEL 0xA


CHECK_EVENTID 0x8 //samson
SVAL 0x3 0x1
BEQ 0x6 0x3 0xC
	CHECK_ALIVE Samson
	SVAL 0x3 0x1
	BNE 0x7 0x3 0xC
	LOAD1 0x1 JustSamson
	LABEL 0x7
LABEL 0x6

CHECK_EVENTID 0x9 //cordelia
SVAL 0x3 0x1
BEQ 0x8 0x3 0xC
	CHECK_ALIVE Cordelia
	SVAL 0x3 0x1
	BNE 0x9 0x3 0xC
	LOAD1 0x1 JustCordelia
	LABEL 0x9
LABEL 0x8

CHECK_TURNS
SVAL 0x3 0x5
BLE 0x3 0x3 0xC
	CALL GazelleIsLate
LABEL 0x3
//Text( Plain_2,Roy1EndingText)
TEXTSTART
SetBackground(Plain_2)
TEXTSHOW Roy1EndingText
TEXTEND
REMA
TEXTSTART
TEXTSHOW Roy1EndingText2
TEXTEND
ClearBackground
REMA
MoveToChapter(Ch10Eirika)
ENDB

MESSAGE Events end at offset CURRENTOFFSET