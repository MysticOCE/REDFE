 //Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

EventPointerTable(0x24,ThisChapter)

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN Units Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0,EnterRaven,3)
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
CharacterEvent(0x7,C6RavenJoins,Roy,Raven)
END_MAIN

LocationBasedEvents:
END_MAIN

MiscBasedEvents:
AREA 0x6 BossIsGone [20,13] [21,13]
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

Units:
UNIT Roy RoyLord 0x0 Level(1, Ally, 0) [2,11] 0x00 0x00 0x0 0x00 [Rapier, Vulnerary] NoAI
UNIT Lukas Cavalier 0x0 Level(4,Ally,0) [1,4] 0x00 0x00 0x0 0x00 [Longsword,Javelin] NoAI
UNIT Alan Cavalier 0x0 Level(4, Ally, 0) [1,11] 0x00 0x00 0x0 0x00 [SteelSword, IronLance, Vulnerary] NoAI
UNIT Dorcas Fighter 0x0 Level(4, Ally, 0) [1,12] 0x00 0x00 0x0 0x00 [SteelAxe,HandAxe,Vulnerary] NoAI
UNIT Anna PegasusKnight 0x0 Level(3, Ally, 0) [1,10] 0x00 0x00 0x0 0x00 [IronLance, GoddessIcon] NoAI
UNIT Salem Monk 0x0 Level(2, Ally, 0) [2,10] 0x00 0x00 0x0 0x00 [Lightning, Shine] NoAI
UNIT Priscilla Troubadour 0x0 Level(2, Ally, 0) [3,11] 0x00 0x00 0x0 0x00 [Heal, Vulnerary] NoAI
UNIT Leila Thief 0x0 Level(2,Ally,0) [4,12] 0x00 0x00 0x0 0x00 [IronSword,Lockpick] NoAI
UNIT

Enemies:
UNIT Beran Myrmidon 0x0 Level(8,Enemy,0) [1,3] 0x00 0x00 0x0 0x0 [SteelSword, Vulnerary] [0x3,0x10,0x0,0x0]
UNIT 0x80 Fighter 0x0 Level(6,Enemy,1) [3,2]  0x00 0x0 0x0 0x0 [SteelAxe] DefaultAI
UNIT 0x80 Mage 0x0 Level(6,Enemy,1) [0,5] 0x0 0x0 0x0 0x0 [Thunder] DefaultAI
UNIT 0x80 Shaman 0x0 Level(7,Enemy,1) [4,3] 0x0 0x0 0x0 0x0 [Flux] DefaultAI
UNIT 0x80 Archer 0x0 Level(6,Enemy,1) [11,3] 0x0 0x0 0x0 0x0 [SteelBow] AttackInRangeAI
UNIT 0x80 Mercenary 0x0 Level(8,Enemy,1) [13,10] 0x0 0x0 0x0 0x0 [IronSword] DefaultAI
UNIT 0x80 Fighter 0x0 Level(8,Enemy,1)[19,11] 0x0 0x0 0x0 0x0 [SteelAxe] AttackInRangeAI
UNIT 0x80 Knight 0x0 Level(7,Enemy,1) [17,0] 0x0 0x0 0x0 0x0 [Javelin] AttackInRangeAI
UNIT 0x80 Mercenary 0x0 Level(6,Enemy,1) [9,2] 0x0 0x0 0x0 0x0 [SteelSword] AttackInRangeAI
UNIT 0x80 Myrmidon 0x0 Level(6,Enemy,1) [14,2] 0x0 0x0 0x0 0x0 [IronSword] AttackInRangeAI
UNIT 0x80 Fighter 0x0 Level(6,Enemy,1) [15,1] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI
UNIT 0x80 Myrmidon 0x0 Level(7,Enemy,1) [21,6] 0x0 0x0 0x0 0x0 [SteelSword] AttackInRangeAI
UNIT

TestLukas:
UNIT Lukas Knight 0x0 Level(4,Ally,0) [1,4] 0x00 0x00 0x0 0x00 [Longsword,Javelin] NoAI
UNIT

RavenUnit:
UNIT Raven Mercenary 0x0 Level(6,NPC,0) [3,0] 0x0 0x0 0x0 0x0 [KillingEdge] DefaultAI
UNIT

EnterRaven:
LOAD1 0x1 RavenUnit
ENUN
TEXTSTART 
TEXTSHOW C6RavenEnters
TEXTEND
REMA
ENDA

C6RavenJoins:
Text(RavenJoinsText)
TurnAlly(Raven)
ENDA

C6EscapePoint:
EscapePoint(21, 13, NoMove)
EscapePointEnd
ENDA

EscapePointPointerTable(0x1, C6EscapePoint)

BossIsGone:
ENUT 0x65
ENDA

BeginningScene:
LOAD2 0x1 Enemies
ENUN
LOAD1 0x1 TestLukas
ENUN
TEXTSTART
SetBackground(Woodland_Night)
TEXTSHOW C6OpeningText1
TEXTEND
REMA
TEXTSTART
TEXTSHOW C6OpeningText2
TEXTEND
ClearBackground
REMA
StartBattle // SVAL rD 0x0 // scripted fight setup
		NormalDamage(0, 0) // SVAL r1 0x20000; SAVETOQUEUE
		MissedAttack(1, 0) // SVAL r1 0x1; SAVETOQUEUE
EndAttack // SVAL r1 0xFFFFFFFF; SAVETOQUEUE
FIG1 Lukas Beran 0x0 //execute fight
TEXTSTART
SetBackground(Woodland_Night)
TEXTSHOW C6OpeningText3
TEXTEND
REMA
ClearBackground
FADU 30
LOAD2 0x1 Units
ENUN
LOAD1 0x1 Enemies
ENUN
//REMA
GotoPrepScreen
NoFade
ENDA

EndingScene:
MoveToChapter(Ch7)
ENDA

MESSAGE Events end at offset CURRENTOFFSET